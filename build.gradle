plugins {
    id 'java'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.25"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

ext {
    cucumberVersion = '7.11.2'
    junitbomVersion = '5.9.2'
    seleniumJavaVersion = '4.1.2'
    junitvintageengineVersion = '5.7.2'
    hamcrestVersion = '2.2'
    webdrivermanagerVersion = '5.3.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation(platform("org.junit:junit-bom:$junitbomVersion"))
    testImplementation(platform("io.cucumber:cucumber-bom:$cucumberVersion"))
    testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
    testImplementation "org.junit.vintage:junit-vintage-engine:$junitvintageengineVersion"
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "io.github.bonigarcia:webdrivermanager:$webdrivermanagerVersion"
    implementation "org.seleniumhq.selenium:selenium-java:$seleniumJavaVersion" // stable version is 3.141.59

}

clean.doFirst {
    delete "$projectDir/target/cucumber-reports", "$projectDir/target/cucumber-html-reports"
}

cucumberReports {
    outputDir = file("$projectDir/target/")
    buildId = '0'
    reports = files("$projectDir/target/cucumber-reports/Cucumber_report.json") // report is generated by this location
}

//approach_1: set override the os and driver
tasks.withType(Test).configureEach {
    systemProperties = [
            defaultos           : System.getProperty('defaultos', 'mac'),
            defaultbrowserdriver: System.getProperty('defaultbrowserdriver', 'firefox')
    ]
}

//approach_2: read from gradle.properties
tasks.register('uiTest', Test) {
    systemProperties project.properties.subMap(["defaultos", "defaultbrowserdriver"])
}

//run with gradle cli: open terminal
// gradle uiTest -Pdefaultos=mac -Pdefaultbrowserdriver=firefox
